import com.android.build.gradle.api.AndroidBasePlugin
import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

buildscript {
  repositories {
    mavenCentral()
    google()
    gradlePluginPortal()
  }

  dependencies {
    classpath libs.androidPlugin
    classpath libs.kotlin.plugin
    classpath libs.japicmpPlugin
    classpath libs.publishPlugin
  }
}

subprojects {
  repositories {
    mavenCentral()
    google()
  }

  plugins.withType(AndroidBasePlugin).configureEach {
    android {
      compileSdk 30

      defaultConfig {
        minSdkVersion 14
        consumerProguardFiles 'consumer-proguard-rules.pro'
      }
      compileOptions {
        targetCompatibility = JavaVersion.VERSION_1_8
        sourceCompatibility = JavaVersion.VERSION_1_8
      }
      lint {
        textReport true
      }
    }
  }

  tasks.withType(KotlinCompile).configureEach {
    compilerOptions {
      jvmTarget = JvmTarget.JVM_1_8
    }
  }

  tasks.withType(Test).configureEach {
    testLogging {
      events "failed"
      exceptionFormat "full"
      showExceptions true
      showStackTraces true
      showCauses true
    }
  }
}
